//@version=5
indicator(title="Volume Price Analysis Indicator", shorttitle="VPA Indicator", overlay=true)

// User Inputs
showHammersShooters = input.bool(true, title="Show Hammers/Shooters")
showDojis = input.bool(true, title="Show Dojis")
showMomentum = input.bool(true, title="Show Momentum Bars")
showOIAndII = input.bool(true, title="Show Outside Inside and Inside Inside Bars")
showHVAnomalies = input.bool(true, title="Show High Volume Anomalies")
showLVAnomalies = input.bool(true, title="Show Low Volume Anomalies")
volumeSMALength = input(50, title="Volume SMA Length")
acceptableVolumeThreshold = input(0.0, title="Acceptable Volume Threshold (% more than average)")
highVolumeThreshold = input(3, title="High Volume Threshold (average times x)")
atrLength = input(14, title="ATR Length")
atrThreshold = input(1.5, title="Momentum Bar ATR Threshold")
hammerBodyToTotalRatio = input(2.0, title="Hammer/Shooter Long Wick to Body Ratio (body times x)")
upperWickToBodyRatio = input(1.0, title="Hammer/Shooter Short Wick to Body Ratio (body times x)")
dojiBodyToTotalRatio = input(0.1, title="Doji Body to High/Low Ratio")
autoHideAutomaticLevels = input.bool(true, title="Hide Automatic Price Levels on Higher Timeframes")
plotPrevMonthHighLow = input.bool(true, title="Plot Previous Month High/Low")
plotPrevWeekHighLow = input.bool(true, title="Plot Previous Week High/Low")
plotPrevDayHighLow = input.bool(true, title="Plot Previous Day High/Low")
plotOpeningPrint = input.bool(true, title="Plot Opening Print")
highPriceLevelColor  = input(color.rgb(8, 153, 129, 30), title="High Price Level Color")
lowPriceLevelColor  = input(color.rgb(242, 54, 69, 30), title ="Low Price Level Color")
openingPrintColor = input(color.new(#fa9c0f, 10), title="Opening Print Color")

// Auto hide some of the automatic levels
timeframe = timeframe.period
monthlyTimeframe = str.contains(str.tostring(timeframe), "M")
weeklyTimeframe = str.contains(str.tostring(timeframe), "W")
dailyTimeframe = str.contains(str.tostring(timeframe), "D")
weeklyOrHigherTimeframe = monthlyTimeframe or weeklyTimeframe
dailyOrHigherTimeframe = weeklyOrHigherTimeframe or dailyTimeframe
higherTimeframeThanOneHour = dailyOrHigherTimeframe or str.tonumber(timeframe) > 60

if monthlyTimeframe or (autoHideAutomaticLevels and dailyOrHigherTimeframe)
    plotPrevMonthHighLow := false
    plotPrevWeekHighLow := false
    plotPrevDayHighLow := false
    plotOpeningPrint := false

if weeklyOrHigherTimeframe
    plotPrevWeekHighLow := false
    plotPrevDayHighLow := false
    plotOpeningPrint := false

if dailyOrHigherTimeframe
    plotPrevDayHighLow := false
    plotOpeningPrint := false

if higherTimeframeThanOneHour
    plotOpeningPrint := false

// Volume calculations
volumeSMA = ta.sma(volume, volumeSMALength)
sufficientVolume = volume > volumeSMA + (volumeSMA * acceptableVolumeThreshold)
highVolume = volume > volumeSMA * highVolumeThreshold
higherVolumeThanPrev = volume > volume[1]
lowerVolumeThanPrev = volume < volume[1]

// Price action calculations
body = math.abs(close - open)
highLow = math.abs(high - low)
prevCandleBody = math.abs(close[1] - open[1])
upperWick = high - math.max(open, close)
lowerWick = math.min(open, close) - low
atrValue = ta.atr(atrLength)

// Determine candle types
isDoji = body <= highLow * dojiBodyToTotalRatio and sufficientVolume
isHammer = lowerWick >= body * hammerBodyToTotalRatio and math.min(open, close) > (high - (high - low) / 2) and sufficientVolume and not isDoji and upperWick <= body * upperWickToBodyRatio
isShooter = upperWick >= body * hammerBodyToTotalRatio and math.max(open, close) < (high - (high - low) / 2) and sufficientVolume and not isDoji and lowerWick <= body * upperWickToBodyRatio
isDoubleInside = high < high[1] and low > low[1] and low[1] > low[2] and high[1] < high[2]
isOutsideInside = high[1] > high[2] and low[1] < low[2] and high < high[1] and low > low[1] and sufficientVolume
bullishMomentumCandle = volume > volume[1] and close > open and math.abs(open - close) > atrValue * atrThreshold and upperWick < body
bearishMomentumCandle = volume > volume[1] and open > close and math.abs(open - close) > atrValue * atrThreshold and lowerWick < body
bullishHighVolumeAnomaly = close < open and close[1] < open[1] and body < prevCandleBody and higherVolumeThanPrev and sufficientVolume
bearishHighVolumeAnomaly = close > open and close[1] > open[1] and body < prevCandleBody and higherVolumeThanPrev and sufficientVolume
bullishLowVolumeAnomaly = close < open and body > prevCandleBody and math.abs(open - close) > atrValue * atrThreshold and lowerVolumeThanPrev and sufficientVolume
bearishLowVolumeAnomaly = close > open and body > prevCandleBody and math.abs(open - close) > atrValue * atrThreshold and lowerVolumeThanPrev and sufficientVolume
bullishVolumeAnomaly = bullishHighVolumeAnomaly or bullishLowVolumeAnomaly
bearishVolumeAnomaly = bearishHighVolumeAnomaly or bearishLowVolumeAnomaly

// Pull in previous day, week, and month high/low
prevMonthHigh = request.security(syminfo.tickerid, "M", high[1], lookahead=barmerge.lookahead_on)
prevMonthLow = request.security(syminfo.tickerid, "M", low[1], lookahead=barmerge.lookahead_on)
prevWeekHigh = request.security(syminfo.tickerid, "W", high[1], lookahead=barmerge.lookahead_on)
prevWeekLow = request.security(syminfo.tickerid, "W", low[1], lookahead=barmerge.lookahead_on)
prevDayHigh = request.security(syminfo.tickerid, "D", high[1], lookahead=barmerge.lookahead_on)
prevDayLow = request.security(syminfo.tickerid, "D", low[1], lookahead=barmerge.lookahead_on)
openingPrint = request.security(syminfo.tickerid, "D", open, lookahead=barmerge.lookahead_on)

// Hide automatic levels
prevMonthHigh := plotPrevMonthHighLow ? prevMonthHigh : na
prevMonthLow := plotPrevMonthHighLow ? prevMonthLow : na
prevWeekHigh := plotPrevWeekHighLow ? prevWeekHigh : na
prevWeekLow := plotPrevWeekHighLow ? prevWeekLow : na
prevDayHigh := plotPrevDayHighLow ? prevDayHigh : na
prevDayLow := plotPrevDayHighLow ? prevDayLow : na
openingPrint := plotOpeningPrint ? openingPrint : na

// Plot prev day, week, and month high/low and the opening print
plot(prevMonthHigh, style=plot.style_circles, color=highPriceLevelColor, linewidth=5, trackprice = true, show_last = 1, offset = -99999)
plot(prevMonthLow, style=plot.style_circles, color=lowPriceLevelColor, linewidth=5, trackprice = true, show_last = 1, offset = -99999)
plot(prevWeekHigh, style=plot.style_circles, color=highPriceLevelColor, linewidth=3, trackprice = true, show_last = 1, offset = -99999)
plot(prevWeekLow, style=plot.style_circles, color=lowPriceLevelColor, linewidth=3, trackprice = true, show_last = 1, offset = -99999)
plot(prevDayHigh, style=plot.style_circles, color=highPriceLevelColor, linewidth=2, trackprice = true, show_last = 1, offset = -99999)
plot(prevDayLow, style=plot.style_circles, color=lowPriceLevelColor, linewidth=2, trackprice = true, show_last = 1, offset = -99999)
plot(openingPrint, style=plot.style_circles, color=openingPrintColor, linewidth=2, trackprice = true, show_last = 1, offset = -99999)

// Plot momentum candles
plotshape(showMomentum and bullishMomentumCandle and not highVolume, style=shape.triangleup, location=location.belowbar, color=color.rgb(51, 129, 201), textcolor=color.rgb(255, 255, 255), text="M")
plotshape(showMomentum and bearishMomentumCandle and not highVolume, style=shape.triangledown, location=location.abovebar, color=color.rgb(51, 129, 201), textcolor=color.rgb(255, 255, 255), text="M")

plotshape(bullishMomentumCandle and highVolume, style=shape.triangleup, location=location.belowbar, color=color.rgb(51, 129, 201), textcolor=color.rgb(255, 255, 255), text="M+")
plotshape(bearishMomentumCandle and highVolume, style=shape.triangledown, location=location.abovebar, color=color.rgb(51, 129, 201), textcolor=color.rgb(255, 255, 255), text="M+")

// Plot hammers and shooters
plotshape(showHammersShooters and isHammer and not highVolume and not bullishVolumeAnomaly and not isDoubleInside and not isOutsideInside, style=shape.triangleup, location=location.belowbar, color=color.rgb(211, 48, 176), textcolor=color.rgb(255, 255, 255), text="H")
plotshape(showHammersShooters and isHammer and highVolume and not bullishVolumeAnomaly and not isDoubleInside and not isOutsideInside, style=shape.triangleup, location=location.belowbar, textcolor=color.rgb(255, 255, 255), color=color.rgb(211, 48, 176), text="H+")

plotshape(showHammersShooters and isHammer and not highVolume and bullishVolumeAnomaly and not isDoubleInside and not isOutsideInside, style=shape.triangleup, location=location.belowbar, color=color.rgb(211, 48, 176), textcolor=color.rgb(255, 255, 255), text="HA")
plotshape(showHammersShooters and isHammer and highVolume and bullishVolumeAnomaly and not isDoubleInside and not isOutsideInside, style=shape.triangleup, location=location.belowbar, textcolor=color.rgb(255, 255, 255), color=color.rgb(211, 48, 176), text="HA+")

plotshape(showHammersShooters and isShooter and not highVolume and not bearishVolumeAnomaly, style=shape.triangledown, location=location.abovebar, color=color.rgb(255, 155, 93), textcolor=color.rgb(255, 255, 255), text="S")
plotshape(showHammersShooters and isShooter and highVolume and not bearishVolumeAnomaly, style=shape.triangledown, location=location.abovebar, textcolor=color.rgb(255, 255, 255), color=color.rgb(255, 155, 93), text="S+")

plotshape(showHammersShooters and isShooter and not highVolume and bearishVolumeAnomaly, style=shape.triangledown, location=location.abovebar, color=color.rgb(255, 155, 93), textcolor=color.rgb(255, 255, 255), text="SA")
plotshape(showHammersShooters and isShooter and highVolume and bearishVolumeAnomaly, style=shape.triangledown, location=location.abovebar, textcolor=color.rgb(255, 255, 255), color=color.rgb(255, 155, 93), text="SA+")

// Plots dojis
plotshape(showDojis and isDoji and not highVolume and not higherVolumeThanPrev, style=shape.triangledown, location=location.abovebar, color=color.rgb(201, 55, 55), textcolor=color.rgb(255, 255, 255), text="D")
plotshape(showDojis and isDoji and highVolume and not higherVolumeThanPrev, style=shape.triangledown, location=location.abovebar, textcolor=color.rgb(255, 255, 255), color=color.rgb(201, 55, 55), text="D+")

plotshape(showDojis and isDoji and not highVolume and higherVolumeThanPrev, style=shape.triangledown, location=location.abovebar, color=color.rgb(201, 55, 55), textcolor=color.rgb(255, 255, 255), text="DA")
plotshape(showDojis and isDoji and highVolume and higherVolumeThanPrev, style=shape.triangledown, location=location.abovebar, textcolor=color.rgb(255, 255, 255), color=color.rgb(201, 55, 55), text="DA+")

// Plot inside outside inside and double inside bars
plotshape(showOIAndII and isOutsideInside and not highVolume and not isHammer, style=shape.triangleup, location=location.belowbar, color=color.rgb(96, 201, 55), textcolor=color.rgb(255, 255, 255), text="OI")
plotshape(showOIAndII and isOutsideInside and highVolume and not isHammer, style=shape.triangleup, location=location.belowbar, textcolor=color.rgb(255, 255, 255), color=color.rgb(96, 201, 55), text="OI+")

plotshape(showOIAndII and isOutsideInside and not highVolume and isHammer, style=shape.triangleup, location=location.belowbar, color=color.rgb(96, 201, 55), textcolor=color.rgb(255, 255, 255), text="HOI")
plotshape(showOIAndII and isOutsideInside and highVolume and isHammer, style=shape.triangleup, location=location.belowbar, textcolor=color.rgb(255, 255, 255), color=color.rgb(96, 201, 55), text="HOI+")

plotshape(showOIAndII and isDoubleInside and not highVolume and not isHammer, style=shape.triangleup, location=location.belowbar, color=color.rgb(96, 201, 55), textcolor=color.rgb(255, 255, 255), text="II")
plotshape(showOIAndII and isDoubleInside and highVolume and not isHammer, style=shape.triangleup, location=location.belowbar, textcolor=color.rgb(255, 255, 255), color=color.rgb(96, 201, 55), text="II+")

plotshape(showOIAndII and isDoubleInside and not highVolume and isHammer, style=shape.triangleup, location=location.belowbar, color=color.rgb(96, 201, 55), textcolor=color.rgb(255, 255, 255), text="HII")
plotshape(showOIAndII and isDoubleInside and highVolume and isHammer, style=shape.triangleup, location=location.belowbar, textcolor=color.rgb(255, 255, 255), color=color.rgb(96, 201, 55), text="HII+")

// Plot volume anomalies
plotshape(showHVAnomalies and bullishHighVolumeAnomaly and not isDoji and not isHammer and not isShooter and not highVolume, location=location.belowbar, color=color.rgb(255, 238, 88), style=shape.triangleup, textcolor=color.rgb(255, 255, 255), text="A")
plotshape(showHVAnomalies and bearishHighVolumeAnomaly and not isDoji and not isHammer and not isShooter and not highVolume, location=location.abovebar, color=color.rgb(255, 238, 88), style=shape.triangledown, textcolor=color.rgb(255, 255, 255), text="A")

plotshape(showHVAnomalies and bullishHighVolumeAnomaly and not isDoji and not isHammer and not isShooter and highVolume, location=location.belowbar, color=color.rgb(255, 238, 88), style=shape.triangleup, textcolor=color.rgb(255, 255, 255), text="A+")
plotshape(showHVAnomalies and bearishHighVolumeAnomaly and not isDoji and not isHammer and not isShooter and highVolume, location=location.abovebar, color=color.rgb(255, 238, 88), style=shape.triangledown, textcolor=color.rgb(255, 255, 255), text="A+")

plotshape(showLVAnomalies and bullishLowVolumeAnomaly and not isDoji and not isHammer and not isShooter and not highVolume, location=location.belowbar, color=color.rgb(255, 238, 88), style=shape.triangleup, textcolor=color.rgb(255, 255, 255), text="A")
plotshape(showLVAnomalies and bearishLowVolumeAnomaly and not isDoji and not isHammer and not isShooter and not highVolume, location=location.abovebar, color=color.rgb(255, 238, 88), style=shape.triangledown, textcolor=color.rgb(255, 255, 255), text="A")

plotshape(showLVAnomalies and bullishLowVolumeAnomaly and not isDoji and not isHammer and not isShooter and highVolume, location=location.belowbar, color=color.rgb(255, 238, 88), style=shape.triangleup, textcolor=color.rgb(255, 255, 255), text="A+")
plotshape(showLVAnomalies and bearishLowVolumeAnomaly and not isDoji and not isHammer and not isShooter and highVolume, location=location.abovebar, color=color.rgb(255, 238, 88), style=shape.triangledown, textcolor=color.rgb(255, 255, 255), text="A+")